apply plugin: 'com.android.library'

android {
  compileSdkVersion 22
  buildToolsVersion "22.0.1"

  defaultConfig {
    minSdkVersion 4
    targetSdkVersion 22
    versionCode 1
    versionName "1.0"
  }
  buildTypes {
    release {
      minifyEnabled false
      proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
    }
  }
  lintOptions {
    abortOnError false
  }
}

dependencies {
  compile fileTree(dir: 'libs', include: ['*.jar'])

  compile 'com.android.support:support-v4:22.1.1'
}

task clearJar(type: Delete) {
  delete "${buildDir}/outputs/${POM_ARTIFACT_ID}.jar"
}

task makeJar(type: Copy) {
  from("${buildDir}/intermediates/bundles/release/")
  into("${buildDir}/outputs/")
  include('classes.jar')
  rename ('classes.jar', "${POM_ARTIFACT_ID}.jar")
}

// only applies if gradle was run with uploadArchives
apply from: 'https://raw.githubusercontent.com/eygraber/gradle-mvn-push/master/gradle-mvn-push.gradle'

afterEvaluate { project ->
  uploadArchives {
    repositories {
      mavenDeployer {
        pom*.whenConfigured { pom ->
          pom.dependencies.removeAll { dep -> dep.artifactId == 'support-v4' }
        }
      }
    }
  }
}
